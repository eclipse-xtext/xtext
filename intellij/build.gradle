allprojects {
	apply plugin: 'maven'
	apply plugin: 'org.xtext.idea-dev'
	
	group = 'org.eclipse.xtext'
	version = '2.8.0-SNAPSHOT'
	
	repositories {
		if (!System.getProperty("JOB_NAME")) {
			mavenLocal()
		}
		mavenCentral()
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	
	ideaDevelopment {
		ideaHome = "${rootDir}/intellij-ce"
	}
}

apply plugin: 'org.xtext.idea-repository'
ideaRepository.rootUrl = 'https://hudson.eclipse.org/xtext/job/xtext-intellij/lastSuccessfulBuild/artifact/git-repo/intellij'

subprojects {
	apply plugin: 'org.xtext.idea-component'
	apply from: "${rootDir}/gradle/eclipse-project-layout.gradle"
	
	configurations.compile {
		exclude module:'cglib'
		exclude module:'icu4j'
		exclude group:'org.ow2.asm'
		exclude module:'org.eclipse.jdt.core'
	}
	
	sourceCompatibility = 1.6
}

def intellijPluginProjects = [
	project(':org.eclipse.xtext.idea'),
	project(':org.eclipse.xtext.xtext.idea'),
	project(':org.eclipse.xtext.xbase.idea'),
	project(':org.eclipse.xtext.idea.sdomain.idea'),
	project(':org.eclipse.xtext.idea.example.entities.idea'),
	project(':org.eclipse.xtend.idea')
]

configure(intellijPluginProjects) {
	apply plugin: 'org.xtext.idea-plugin'
}