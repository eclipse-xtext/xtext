module org.eclipse.xtext.testlanguages.GenerateTestLanguages

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {

	component = XtextGenerator {
		configuration = {
			project = model.project.StandardProjectConfig {
				baseName = "org.eclipse.xtext.testlanguages"
				rootPath = rootPath

				eclipsePlugin = {
					enabled = true
				}
				ideaPlugin = {
					enabled = true
					root = "${rootPath}/../intellij/org.eclipse.xtext.testlanguages.idea"	
				}
				
				createEclipseMetaData = true
			}
			code = {
				lineDelimiter = "\n"
				preferXtendStubs = false
			}
		}

		language = @backtracking.BeeLangTestLanguage {}
		language = @backtracking.SimpleBeeLangTestLanguage {}
		language = @backtracking.ExBeeLangTestLanguage {}
		language = @noJdt.NoJdtTestLanguage {}
	}
}
