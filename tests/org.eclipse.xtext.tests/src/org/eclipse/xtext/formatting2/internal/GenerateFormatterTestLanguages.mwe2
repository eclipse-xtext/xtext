/*******************************************************************************
 * Copyright (c) 2014 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtext.parsetree.formatter.GenerateFormatterTestLanguages

import org.eclipse.emf.mwe2.runtime.workflow.Workflow
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.*
import org.eclipse.xtext.generator.*

var projectName = "org.eclipse.xtext.tests"
var runtimeProject = "../${projectName}"
var lineDelimiter = '\n'

Workflow {
	bean = StandaloneSetup {
		platformUri = "${runtimeProject}/.."
	}

	component = DirectoryCleaner {
		directory = "${runtimeProject}/src-gen/org/eclipse/xtext/formatting2"
	}

	component = Generator auto-inject {
		pathRtProject = runtimeProject
		pathTestProject = runtimeProject
		projectNameRt = projectName
		language = {
			uri = "classpath:/org/eclipse/xtext/formatting2/internal/FormatterTestLanguage.xtext"
			fragment = @ TestLanguagesFragments auto-inject {}
			fragment = generator.junit.Junit4Fragment {}
		}
		language = {
			uri = "classpath:/org/eclipse/xtext/formatting2/regionaccess/internal/RegionAccessTestLanguage.xtext"
			fragment = @ TestLanguagesFragments auto-inject {}
			fragment = generator.junit.Junit4Fragment {}
		}
	}
}