plugins.withType(JavaBasePlugin) {
	sourceCompatibility = 1.6
}

tasks.withType(JavaCompile) {
	options.encoding = 'ISO-8859-1'
}

/*
 * Skip java compiler completely on local machines,
 * because we all have Eclipse running, which
 * already compiles the code. Just reuse the classes
 * from the bin folder
 */

if (!isBuildServer) {
	project.gradle.addListener LocalBuildWarning.INSTANCE

	plugins.withType(JavaPlugin) {
		compileJava.enabled = false
		processResources.enabled = false

		task copyFromBin(type: Sync) {
		    from 'bin'
		    into 'build/classes/main'
		}

		classes.dependsOn(copyFromBin)
	}
}

class LocalBuildWarning extends BuildAdapter {
	static def INSTANCE = new LocalBuildWarning()

	void buildFinished(BuildResult result) {
		if (result.failure) {
			result.gradle.rootProject.logger.warn("This was a local build, make sure Eclipse is running and successfully compiled the code. Otherwise use the '-PbuildServer' option")
		}
	}
}
