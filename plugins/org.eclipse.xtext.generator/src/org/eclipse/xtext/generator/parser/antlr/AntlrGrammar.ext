/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 *******************************************************************************/
import org::eclipse::xtext;
import org::eclipse::emf::ecore;

extension org::eclipse::xtext::generator::grammarAccess::GrammarAccess reexport;
extension org::eclipse::xtext::Extensions reexport;
extension org::eclipse::xtext::GrammarUtil reexport;
extension org::eclipse::xtext::EcoreUtils;

ruleName(ParserRule this) : 'rule'+name;
ruleName(EnumRule this) : 'rule'+name;
ruleName(TerminalRule this) : 'RULE_'+name.toUpperCase();
ruleName(AbstractRule this) : "Unsupported";

predicated(AbstractElement this):
  this.predicated
;

predicated(Assignment this):
  this.predicated || this.terminal.predicated()
;

predicated(RuleCall this):
  this.predicated || Group.isInstance(this.rule.alternatives) && ((Group)this.rule.alternatives).elements.first().predicated()
;

predicatedElement(AbstractElement this):
  this
;

predicatedElement(Assignment this):
  if predicated then this else this.terminal.predicatedElement()
;

predicatedElement(RuleCall this):
  if predicated then this else ((Group)this.rule.alternatives).elements.first().predicatedElement()
;

entryRuleName(ParserRule this) : 'entryRule'+name;

setOrAdd(Action this) : if operator=='+=' then 'add' else 'set';


setOrAdd(Assignment this) : if operator=='+=' then 'add' else 'set';

isCalled(Grammar grammar, AbstractRule this) :
	let x = grammar.allRules() :
		x.indexOf(this)==0 || 
		x.allContents().flatten().typeSelect(RuleCall).select(call|call.rule!=null).rule.contains(this);

localVar(Assignment this, AbstractElement terminal) : 
	terminal.localVar();
	
localVar(RuleCall this) : 'this_'+ rule.localVarSuffix();
localVar(AbstractElement this) : 'other';
localVar(EnumLiteralDeclaration this) : 'enumLiteral';

localVarSuffix(AbstractRule this): 'token';
localVarSuffix(ParserRule this): isDatatypeRule() ? 'dataType' : 'object';

List[AbstractElement] contentsAsList(ParserRule this):
	alternatives.contentsAsList();
	
List[AbstractElement] contentsAsList(AbstractElement this):
	{this};
	
List[AbstractElement] contentsAsList(CompoundElement this):
	this.elements.contentsAsList();

List[AbstractElement] contentsAsList(UnorderedGroup this):
	{this}.addAll(this.elements.contentsAsList());
	
toStringLiteral(AbstractElement this) :
	"null";
	
String toAntlrString(String this):
	JAVA org.eclipse.xtext.generator.parser.antlr.AntlrGrammarGenUtil.toAntlrString(java.lang.String);

String toJavaStringImpl(String this):
	JAVA org.eclipse.xtext.generator.parser.antlr.AntlrGrammarGenUtil.toStringInAntlrAction(java.lang.String);
	
toStringLiteral(RuleCall this) :
	rule!=null ? this.rule.toStringLiteral() : "null";

toStringLiteral(AbstractRule this) :
	'"'+name+'"';
	
toStringLiteral(ParserRule this) :
	isDatatypeRule() ? '"'+name+'"' : "null";

toStringLiteral(Keyword this) :
	'"'+this.value.toJavaStringImpl()+'"';
	
toStringLiteralInAssignment(Keyword this) :
	"null";

toStringLiteralInAssignment(AbstractElement this) :
	toStringLiteral();
	
String toLexerBody(TerminalRule this):
	JAVA org.eclipse.xtext.generator.parser.antlr.TerminalRuleToLexerBody.toLexerBody(org.eclipse.xtext.TerminalRule);
	
String objectURI(Grammar grammar, EObject object) :
	JAVA org.eclipse.xtext.generator.parser.antlr.AntlrGrammarGenUtil.getClasspathURI(org.eclipse.xtext.Grammar, org.eclipse.emf.ecore.EObject);

mustBeParenthesized(AbstractElement this) : true;
mustBeParenthesized(Group this) : true;
mustBeParenthesized(UnorderedGroup this) : true;
mustBeParenthesized(Alternatives this) : true;
mustBeParenthesized(EnumLiteralDeclaration this) : true;
mustBeParenthesized(Keyword this) : predicated() || cardinality!=null;
mustBeParenthesized(RuleCall this) : predicated() || cardinality!=null;

String grammarElementAccess(EObject object):
	"Unsupported : grammarElementAccess for: " + object.toString(); 
	
String grammarElementAccess(AbstractRule this):
	gaRuleAccessor(); 

String grammarElementAccess(AbstractElement element):
	gaRuleElementAccessor(element);

String grammarElementAccessInRule(EObject object):
	"Unsupported : grammarElementAccessInRule for: " + object.toString();
		
String grammarElementAccessInRule(AbstractElement element):
	"elementAccess." + element.gaElementAccessor();
	
String grammarElementAccessInRule(AbstractRule rule):
	"elementAccess.getRule()";

List[String] initialHiddenTokens(Grammar this):
  if (definesHiddenTokens) then 
  	hiddenTokens.ruleName() 
  else if (usedGrammars.size == 1) then 
  	initialHiddenTokens(usedGrammars.first()) 
  else {};
  
