module org.eclipse.xtend.core.GenerateXtend

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*
import org.eclipse.xtext.generator.parser.antlr.*
import org.eclipse.xtext.resource.XtextResourceSet 

var grammarURI = "classpath:/org/eclipse/xtend/core/Xtend.xtext"
var file.extensions = "xtend"
var projectName = "org.eclipse.xtend.core"
var projectPath = "../${projectName}"

var lineDelimiter = "\n"
var uiProjectName = "org.eclipse.xtend.ide"
var uiProjectPath = "../${uiProjectName}"
var backtrack = false
var memoize = false

var ideaProjectName = "org.eclipse.xtend.idea"
var ideaProjectPath = "../../intellij/${ideaProjectName}"

Workflow {
	bean = StandaloneSetup {
		ignoreBrokenProjectFiles = true
		scanClassPath = true
		registerGenModelFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"
	}
	
	bean = StandaloneSetup {
		resourceSet = XtextResourceSet : resourceSet {}
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.ecore"
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xtype.ecore"
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.ecore"
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.xbase/model/XAnnotations.ecore"
		registerEcoreFile = "platform:/resource/${projectName}/model/Xtend.ecore"
	}
	
	component = DirectoryCleaner {
		directory = "${projectPath}/emf-gen"
	}

	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
		genModel = "platform:/resource/${projectName}/model/Xtend.genmodel"
		srcPath ="platform:/resource/${projectName}/src"
		srcPath ="platform:/resource/org.eclipse.xtext.common.types/src"
		srcPath ="platform:/resource/org.eclipse.xtext.xbase/src"
		lineDelimiter = lineDelimiter
	}
	
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
		genModel = "platform:/resource/${projectName}/model/RichStrings.genmodel"
		srcPath ="platform:/resource/${projectName}/src"
		srcPath ="platform:/resource/org.eclipse.xtext.common.types/src"
		srcPath ="platform:/resource/org.eclipse.xtext.xbase/src"
		lineDelimiter = lineDelimiter
	}

	component = DirectoryCleaner {
		directory = "${projectPath}/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${uiProjectPath}/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${ideaProjectPath}/src-gen"
	}
	
	component = parser.JFlexLoader : jFlexMain {}

	component = Generator auto-inject {
		pathRtProject = projectPath
		pathUiProject = uiProjectPath
		projectNameRt = projectName
		projectNameUi = uiProjectName
		encoding = "ISO-8859-1"
		naming = {
			uiBasePackage = "org.eclipse.xtend.ide"
			grammarId = "org.eclipse.xtend.core.Xtend"
			lineDelimiter = lineDelimiter
		}
		language = {
			forcedResourceSet = resourceSet
			uri = grammarURI
			fileExtensions = file.extensions
			fragment = grammarAccess.GrammarAccessFragment auto-inject {}
			fragment = serializer.SerializerFragment {
				// generateDebugData = true
			}
			fragment = resourceFactory.ResourceFactoryFragment {
				fileExtensions = file.extensions
			}
			fragment = parser.ParserGenerator auto-inject {
				antlrParam = "-Xconversiontimeout" antlrParam = "20000"
				options = auto-inject {}
			}
			fragment = DebugAntlrGeneratorFragment auto-inject {
				options = auto-inject {}
			}
			fragment = validation.JavaValidatorFragment {}
			fragment = generator.GeneratorFragment {}
			fragment = scoping.ImportNamespacesScopingFragment {}
			fragment = builder.BuilderIntegrationFragment {}
			fragment = quickfix.QuickfixProviderFragment {}
			fragment = labeling.LabelProviderFragment {
				generateStub = false
			}
			fragment = outline.OutlineTreeProviderFragment {}
			fragment = outline.QuickOutlineFragment {}
			fragment = compare.CompareFragment {
				fileExtensions = file.extensions
			}
			fragment = contentAssist.JavaBasedContentAssistFragment {}
			fragment = parser.ContentAssistParserGenerator auto-inject {
				removeBacktrackingGuards = true
				lookaheadThreshold = '5'
				antlrParam = "-Xconversiontimeout" antlrParam = "20000"
				options = auto-inject {
					classSplitting = true
				}
				partialParsing = true
			}
			fragment = refactoring.RefactorElementNameFragment {
				useJdtRefactoring = true
			}
			fragment = types.TypesGeneratorFragment {}
			fragment = xbase.XbaseGeneratorFragment {
				generateXtendInferrer = false
			}
			fragment = templates.CodetemplatesGeneratorFragment {
				suppressRestriction = false
			}
			fragment = parser.JFlexGeneratorFragment auto-inject {
				patternPath = "/org/eclipse/xtend/core/parser/antlr/internal/patterns.incl"
				additionalRulePath = "/org/eclipse/xtend/core/parser/antlr/internal/rules.incl"
			}
			fragment = parser.JFlexGeneratorFragment auto-inject {
				ui = true
				patternPath = "/org/eclipse/xtend/core/parser/antlr/internal/patterns.incl"
				additionalRulePath = "/org/eclipse/xtend/core/parser/antlr/internal/rules.incl"
			}
	        fragment = parser.XtendAntlrIDEAGeneratorFragment auto-inject {
				antlrParam = "-Xconversiontimeout" antlrParam = "20000"
			}
    		fragment = org.eclipse.xtext.idea.generator.IdeaPluginGenerator auto-inject {
    			runtimeProjectName = projectName
    			runtimeProjectPath = projectPath
	            fileExtensions = file.extensions
	            typesIntegrationRequired = true
	        }
		}
	}
}
