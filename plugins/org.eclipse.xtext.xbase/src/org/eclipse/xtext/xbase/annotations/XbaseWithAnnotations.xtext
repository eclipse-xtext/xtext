/*******************************************************************************
 * Copyright (c) 2011 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/Xtext/Xbase/XAnnotations"

XAnnotation :
	{XAnnotation}
	'@' annotationType=[types::JvmAnnotationType | QualifiedName] (=>'('
		(
			elementValuePairs+=XAnnotationElementValuePair (',' elementValuePairs+=XAnnotationElementValuePair)* 
		|	value=XAnnotationElementValue
		)?
	')')?
;

XAnnotationElementValuePair :
	element=[types::JvmOperation|ValidID] '=' value=XAnnotationElementValue	
;

XAnnotationElementValue returns xbase::XExpression:
	XAnnotationElementValueNoList | XAnnotationListLiteral;

XAnnotationElementValueNoList returns xbase::XExpression:
	XAnnotationMultiplicativeExpression (=>({xbase::XBinaryOperation.leftOperand=current} feature=[types::JvmIdentifiableElement|OpAdd])
	rightOperand=XAnnotationMultiplicativeExpression)*;

XAnnotationMultiplicativeExpression returns xbase::XExpression:
	XAnnotationUnaryOperation (=>({xbase::XBinaryOperation.leftOperand=current} feature=[types::JvmIdentifiableElement|OpMulti]) rightOperand=XAnnotationUnaryOperation)*;

XAnnotationUnaryOperation returns xbase::XExpression:
	{xbase::XUnaryOperation} feature=[types::JvmIdentifiableElement|OpUnary] operand=XAnnotationUnaryOperation
	| XAnnotationPrimaryExpression;

XAnnotationPrimaryExpression returns xbase::XExpression :
	XAnnotation |
	XStringLiteral |
	XBooleanLiteral |
	XNumberLiteral |
	XTypeLiteral |
	XAnnotationValueMemberFieldReference |
	XAnnotationParameterizedExpression
;

XAnnotationParameterizedExpression returns xbase::XExpression:
	'(' XAnnotationElementValueNoList ')'
;

XAnnotationListLiteral returns xbase::XListLiteral:
	{xbase::XListLiteral} '#' '[' (elements+=XAnnotationElementValueNoList (',' elements+=XAnnotationElementValueNoList )*)? ']'
;

XAnnotationValueMemberFieldReference returns xbase::XExpression :
	XAnnotationValueFieldReference (
		{xbase::XMemberFeatureCall.memberCallTarget=current} ("."|explicitStatic?="::")
		feature=[types::JvmIdentifiableElement|IdOrSuper] 
	)*
;

XAnnotationValueFieldReference returns xbase::XExpression :
	{xbase::XFeatureCall} 
	feature=[types::JvmIdentifiableElement|IdOrSuper] 
;
